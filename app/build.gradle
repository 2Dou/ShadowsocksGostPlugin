apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.github.shadowsocks.plugin.gost"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 281
        versionName "2.8.1"
    }
    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', "arm64-v8a"
            universalApk true
        }
    }
    signingConfigs {
        releaseConfig {
            storeFile file("../../xausky.jks")
            storePassword System.getenv("KEYSTORE_PASS")
            keyAlias System.getenv("ALIAS_NAME")
            keyPassword System.getenv("ALIAS_PASS")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.releaseConfig
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'com.github.shadowsocks:plugin:1.2.0'
}

task buildGoLibrary(type: Exec) {
    environment ([ANDROID_NDK_ROOT: android.ndkDirectory])
    commandLine ('sh', 'src/main/go/build.sh')
    inputs.files 'src/main/go/go.mod', 'src/main/go/go.sum'
    outputs.dir 'src/main/jniLibs'
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("preDebugBuild") || theTask.name.equals("preReleaseBuild")) {
        theTask.dependsOn "buildGoLibrary"
    }
}
